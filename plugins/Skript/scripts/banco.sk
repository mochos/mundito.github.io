options:
    banco: "&a&l[Banco Central] &r&f"

#Abrir banco con cartel
on rightclick on sign:
    if line 2 of event-block is "&2&l[Banco]":
        cancel event
        banco(player)

#Abrir banco con aldeano
on rightclick on villager:
    if name of clicked entity contains "&a&lBanco":
        cancel event
        banco(player)

#Admin crear banco
command /crearbanco:
    permission: mundito.crearbanco
    trigger:
        set {_target} to target block of player
        if {_target} is a sign:
            set line 2 of block at location of {_target} to "&2&l[Banco]"
            send "%{@banco}%&aCajero creado con éxito."
        else:
            send "%{@banco}%&cNo estás mirando un cartel."

#Cartel dinero en circulación
on rightclick on sign:
    if line 1 of event-block is "&2&l[Circulando]":
        cancel event
        load yaml "plugins/Skript/data/economia.yml" as "economia"
        set {_ceitiles} to yaml value "ceitiles.total" from "economia"
        set line 2 of event-block to "&bC: $%{_ceitiles}%"
        set {_dinero} to yaml value "dinero.total" from "economia"
        set line 3 of event-block to "&bB: $%{_dinero}%"
        wait 60 seconds
        set line 2 of event-block to " "
        set line 3 of event-block to "&bConsultar"

command /logeconomia:
    permission: mundito.admin
    trigger:
        logEco()
        send "Se han actualizado los datos de la economía." to player

function logEco():
    load yaml "plugins/Skript/data/economia.yml" as "economia"
    set {_total} to 0
    loop all offline players:
        set {_tempPlayer} to "%loop-value%"
        set {_tempBalance} to "%balance of loop-value%"
        replace all "$" with "" in {_tempBalance}
        replace all "," with "" in {_tempBalance}
        set {_tempBalance} to {_tempBalance} parsed as integer
        set yaml value "dinero.%{_tempPlayer}%" from "economia" to {_tempBalance}
        add {_tempBalance} to {_total}
        wait 1 tick
    #broadcast "%{@banco}%&aTotal: %{_total}%"
    set yaml value "dinero.total" from "economia" to {_total}
    save yaml "economia"
    unload yaml "economia"

#Depositar en el banco
function depositar(p: player, num: number):
    if {_p} has {_num} of sunflower named "&6&lCeitil" with lore "&7Banco Central":
        remove {_num} of sunflower named "&6&lCeitil" from {_p}'s inventory
        execute console command "eco give %{_p}% %{_num}%"
        send "%{@banco}%Has depositado &6&l$%{_num}% ceitiles &aen tu cuenta" to {_p}
        play sound "entity.experience_orb.pickup" with volume 1 to {_p}
        banco({_p})
    else:
        send "%{@banco}%&cNo tienes suficientes &6&lceitiles &cpara depositar." to {_p}
        play sound "entity.villager.no" with volume 1 to {_p}

#Retirar en el banco
function retirar(p: player, num: number):
    if {_p}'s money is greater than or equal to {_num}:
        execute console command "eco take %{_p}% %{_num}%"
        give {_num} of sunflower named "&6&lCeitil" with lore "&7Banco Central" to {_p}'s inventory
        play sound "entity.experience_orb.pickup" with volume 1 to {_p}
        send "%{@banco}%Has retirado &6&l$%{_num}% ceitiles &ade tu cuenta" to {_p}
        banco({_p})
    else:
        send "%{@banco}%&cNo tienes suficientes dinero en tu cuenta para retirar." to {_p}
        play sound "entity.villager.no" with volume 1 to {_p}

#Evita plantar los ceitiles
on place of sunflower:
    if name of player's tool contains "&6&lCeitil":
        cancel event

#Registro de emisión de moneda
command /emitirceitil <text> <number>:
    permission: mundito.emitirceitil
    trigger:
        set {_player} to arg-1
        set {_cantidad} to arg-2
        load yaml "plugins/Skript/data/economia.yml" as "economia"
        set {_total} to yaml value "ceitiles.total" from "economia"
        set {_totalplayer} to yaml value "ceitiles.%{_player}%" from "economia"
        if {_total} is not set:
            set {_total} to 0
        add {_cantidad} to {_total}
        if {_totalplayer} is not set:
            set {_totalplayer} to 0
        add {_cantidad} to {_totalplayer}
        set yaml value "ceitiles.total" from "economia" to {_total}
        set yaml value "ceitiles.%{_player}%" from "economia" to {_totalplayer}
        wait 1 tick
        save yaml "economia"
        #send "%{@banco}%&aTu interacción ha sido registrada. Total: %{_total}%" to player
        unload yaml "economia"

#Interfaz del banco
function banco(p: player):
    load yaml "plugins/Skript/data/economia.yml" as "economia"
    set {_total} to yaml value "ceitiles.total" from "economia"
    set {_lomas1} to yaml value "ceitiles.lorspi" from "economia"
    set {_lomas2} to yaml value "ceitiles._lorspi" from "economia"
    set {_lomast} to {_lomas1} + {_lomas2}
    set {_el0town1} to yaml value "ceitiles.El0tito" from "economia"
    set {_el0town2} to yaml value "ceitiles.Cr081s: 0" from "economia"
    set {_el0townt} to {_el0town1} + {_el0town2}
    set {_transacciones} to a new chest inventory with 4 row named "&a&lBanco Central"
    set slot 10 of {_transacciones} to book named "&6&lSaldo en tu cuenta" with lore " &f%balance of {_p}%"
    set slot 19 of {_transacciones} to book named "&6&lIndicadores económicos" with lore "&f ▶ Valor esmeralda: $10" and "&f ▶ Fundar ciudad: $100" and "&f ▶ Fundar outpost: $300" and "&f ▶ Valor del chunk: $300" and "&f ▶ Ceitiles emitidos: $%{_total}%" and "&7    [ Lomas: $%{_lomast}% | El0town: $%{_el0townt}% ]"
    #Retirar
    set slot 12 of {_transacciones} to sunflower named "&6&lRetirar 1 ceitil" with lore "&fPoner 1 ceitil en tu inventario"
    set slot 13 of {_transacciones} to sunflower named "&6&lRetirar 10 ceitiles" with lore "&fPoner 10 ceitiles en tu inventario"
    set slot 14 of {_transacciones} to sunflower named "&6&lRetirar 100 ceitiles" with lore "&fPoner 100 ceitiles en tu inventario"
    set slot 15 of {_transacciones} to sunflower named "&6&lRetirar 500 ceitiles" with lore "&fPoner 500 ceitiles en tu inventario"
    #Depositar
    set slot 21 of {_transacciones} to sunflower named "&6&lDepositar 1 ceitil" with lore "&fPoner 1 ceitil en tu cuenta bancaria"
    set slot 22 of {_transacciones} to sunflower named "&6&lDepositar 10 ceitiles" with lore "&fPoner 10 ceitile en tu cuenta bancaria"
    set slot 23 of {_transacciones} to sunflower named "&6&lDepositar 100 ceitiles" with lore "&fPoner 100 ceitiles en tu cuenta bancaria"
    set slot 24 of {_transacciones} to sunflower named "&6&lDepositar 500 ceitiles" with lore "&fPoner 500 ceitiles en tu cuenta bancaria"
    open {_transacciones} to {_p}

#Transacciones
on inventory click:
    name of event-inventory is "&a&lBanco Central"
    cancel event
    if index of event-slot is 12:
        retirar(player, 1)
    if index of event-slot is 13:
        retirar(player, 10)
    if index of event-slot is 14:
        retirar(player, 100)
    if index of event-slot is 15:
        retirar(player, 500)
    if index of event-slot is 21:
        depositar(player, 1)
    if index of event-slot is 22:
        depositar(player, 10)
    if index of event-slot is 23:
        depositar(player, 100)
    if index of event-slot is 24:
        depositar(player, 500)

#Interfaz de divisas
command /divisa:
    permission: mundito.divisa
    trigger:
        set {_divisa} to a new chest inventory with 4 row named "&a&lCambio de divisa"
        #1 esmeralda
        set slot 12 of {_divisa} to sunflower named "&6&lRetirar 10 ceitiles" with lore "&fPoner 10 ceitiles en tu inventario"
        set slot 21 of {_divisa} to sunflower named "&6&lDepositar 10 ceitiles" with lore "&fPoner 10 ceitiles en tu cuenta bancaria"
        #10 esmeraldas
        set slot 13 of {_divisa} to sunflower named "&6&lRetirar 100 ceitiles" with lore "&fPoner 100 ceitiles en tu inventario"
        set slot 22 of {_divisa} to sunflower named "&6&lDepositar 100 ceitiles" with lore "&fPoner 100 ceitiles en tu cuenta bancaria"
        #50 esmeraldas
        set slot 14 of {_divisa} to sunflower named "&6&lRetirar 500 ceitiles" with lore "&fPoner 500 ceitiles en tu inventario"
        set slot 23 of {_divisa} to sunflower named "&6&lDepositar 500 ceitiles" with lore "&fPoner 500 ceitiles en tu cuenta bancaria"
        open {_divisa} to player

#Cambio de divisa
on inventory click:
    name of event-inventory is "&a&lCambio de divisa"
    cancel event
    if index of event-slot is 12:
        #send "Retirar 10"
        if player has 1 of emerald:
            remove 1 of emerald from player's inventory
            give 10 of sunflower named "&6&lCeitil" with lore "&7Banco Central" to player's inventory
            send "%{@banco}%&aHas depositado &6&l$10 ceitiles &aen tu inventario." to player
            execute console command "/emitirceitil %player% 10"
            play sound "entity.experience_orb.pickup" with volume 1 to the player
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player
            play sound "entity.villager.no" with volume 1 to player
    if index of event-slot is 21:
        #send "Depositar 10"
        if player has 1 of emerald:
            remove 1 of emerald from player's inventory
            execute console command "eco give %player% 10"
            send "%{@banco}%&aHas depositado &6&l$10 ceitiles &aen tu cuenta" to player
            execute console command "/emitirceitil %player% 10"
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player
    if index of event-slot is 13:
        #send "Retirar 100"
        if player has 10 of emerald:
            remove 10 of emerald from player's inventory
            give 100 of sunflower named "&6&lCeitil" with lore "&7Banco Central" to player's inventory
            send "%{@banco}%&aHas depositado &6&l$100 ceitiles &aen tu inventario" to player
            execute console command "/emitirceitil %player% 100"
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player
    if index of event-slot is 22:
        #send "Depositar 100"
        if player has 10 of emerald:
            remove 10 of emerald from player's inventory
            execute console command "eco give %player% 100"
            send "%{@banco}%&aHas depositado &6&l$100 ceitiles &aen tu cuenta." to player
            execute console command "/emitirceitil %player% 100"
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player
    if index of event-slot is 14:
        #send "Retirar 500"
        if player has 50 of emerald:
            remove 50 of emerald from player's inventory
            give 500 of sunflower named "&6&lCeitil" with lore "&7Banco Central" to player's inventory
            send "%{@banco}%&aHas depositado &6&l$500 ceitiles &aen tu inventario." to player
            execute console command "/emitirceitil %player% 500"
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player
    if index of event-slot is 23:
        #send "Depositar 500"
        if player has 50 of emerald:
            remove 50 of emerald from player's inventory
            execute console command "eco give %player% 500"
            send "%{@banco}%&aHas depositado &6&l$500 ceitiles &aen tu cuenta." to player
            execute console command "/emitirceitil %player% 500"
        else:
            send "%{@banco}%&cNo tienes suficientes &6&lcesmeraldas &cpara cambiar." to player


#Registro de economía: Que cada 40 minutos haga un recorrido de todos los blances de los usuarios y lo deje en el YML.
#dinero.total, dinero.%player%, sume el total de todos y el individual en lista.